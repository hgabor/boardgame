# http://en.wikipedia.org/wiki/Peg_solitaire

Settings (
    PlayerCount: 1;
    PieceTypes: piece;
    BoardDimensions: {7, 7};
)

StartingBoard (
    Valid (
        (x >= 3 And x <= 5) Or
        (y >= 3 And y <= 5);
    )
    Player(1) (
        piece: {1, 3}; piece: {1, 4}; piece: {1, 5};
        piece: {2, 3}; piece: {2, 4}; piece: {2, 5};
        piece: {3, 1}; piece: {3, 2}; piece: {3, 3};
        piece: {3, 4}; piece: {3, 5}; piece: {3, 6};
        piece: {3, 7};
        piece: {4, 1}; piece: {4, 2}; piece: {4, 3};
                       piece: {4, 5}; piece: {4, 6};
        piece: {4, 7};
        piece: {5, 1}; piece: {5, 2}; piece: {5, 3};
        piece: {5, 4}; piece: {5, 5}; piece: {5, 6};
        piece: {5, 7};
        piece: {6, 3}; piece: {6, 4}; piece: {6, 5};
        piece: {7, 3}; piece: {7, 4}; piece: {7, 5};
    )
)

Moves (
    piece: {_, _} -> Empty {x+2, y} If Not IsEmpty({x+1, y}) Then
        RemovePiece({x+1, y});
    End;
    piece: {_, _} -> Empty {x-2, y} If Not IsEmpty({x-1, y}) Then
        RemovePiece({x-1, y});
    End;
    piece: {_, _} -> Empty {x, y+2} If Not IsEmpty({x, y+1}) Then
        RemovePiece({x, y+1});
    End;
    piece: {_, _} -> Empty {x, y-2} If Not IsEmpty({x, y-1}) Then
        RemovePiece({x, y-1});
    End;
)

Events (
    Player(1).FinishedMove (
        If Count(Pieces) = 1 Then
            Win(Player(1));
        End
    )
    Player(1).CannotMove (
        Lose(Player(1));
    )
)
